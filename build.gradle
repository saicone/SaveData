plugins {
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'net.raphimc.class-token-replacer' version '1.1.7' apply false
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven { url = uri('https://jitpack.io') }
    }
}

subprojects {
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencies {
        // MCode
        implementation libs.mcode.bootstrap
        compileOnly libs.mcode.module.task
        compileOnly libs.mcode.module.lang

        // Implementation
        compileOnly libs.settings
        compileOnly libs.guava

        compileOnly libs.annotations

        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(':common')
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    exclude 'com/saicone/mcode/**'
    exclude 'com/saicone/savedata/core/**'
    exclude 'com/saicone/savedata/module/**'
    exclude 'com/saicone/savedata/util/**'
    exclude 'com/saicone/savedata/SaveData.*'
    exclude '**/*.yml'
}

publishing {
    publications {
        api(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.shadow
        }
    }
}