plugins {
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.5'
}

allprojects {
    apply plugin: 'java'

    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencies {
        // MCode
        implementation libs.mcode.bootstrap
        compileOnly libs.mcode.module.task
        compileOnly libs.mcode.module.lang

        // Implementation
        compileOnly 'com.saicone.settings:settings:1.0.1'
        compileOnly 'com.google.guava:guava:33.4.8-jre'

        compileOnly 'org.jetbrains:annotations:26.0.2'

        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(':common')
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    exclude 'com/saicone/mcode/**'
    exclude 'com/saicone/savedata/core/**'
    exclude 'com/saicone/savedata/module/**'
    exclude 'com/saicone/savedata/util/**'
    exclude 'com/saicone/savedata/SaveData.*'
    exclude '**/*.yml'
}

publishing {
    publications {
        api(MavenPublication) {
            groupId project.group
            artifactId 'API'
            version project.version
            from components.shadow
        }
    }
}